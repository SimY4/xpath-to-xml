plugins {
    id 'cz.alenkacz.gradle.scalafmt' version '1.14.0' apply false
    id 'cz.augi.gradle.wartremover' version '0.10.0' apply false
    id 'info.solidsoft.pitest' version '1.5.0' apply false
    id 'java-platform'
    id 'me.champeau.gradle.jmh' version '0.5.0' apply false
    id 'net.ltgt.errorprone' version '1.1.1' apply false
    id 'net.researchgate.release' version '2.8.1'
    id 'ru.vyarus.animalsniffer' version '1.5.0' apply false
}

ext {
    jacocoVersion = '0.8.5'

    def buildTimeAndDate = new Date()
    buildDate = buildTimeAndDate.format("yyyy-MM-dd")
    buildTime = buildTimeAndDate.format("HH:mm:ss.SSSZ")
}

subprojects {
    apply plugin: 'java-library'
    apply from: rootProject.file('gradle/dependencies.gradle')
    apply from: rootProject.file('gradle/check-checkstyle.gradle')
    apply from: rootProject.file('gradle/check-codecoverage.gradle')
    apply from: rootProject.file('gradle/check-errorprone.gradle')
    apply from: rootProject.file('gradle/publish-jar.gradle')
    apply from: rootProject.file('gradle/publish-maven.gradle')

    sourceCompatibility = JavaVersion.VERSION_11

    repositories {
        mavenCentral()
    }

    configurations {
        internal {
            visible = false
            canBeConsumed = false
            canBeResolved = false
        }
        compileClasspath.extendsFrom internal
        runtimeClasspath.extendsFrom internal
        testCompileClasspath.extendsFrom internal
        testRuntimeClasspath.extendsFrom internal
    }

    dependencies {
        internal platform(rootProject)
    }

    java {
        modularity.inferModulePath.set(true)

        withJavadocJar()
        withSourcesJar()
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << '-Xlint:all' << '-Werror' << '--release' << '11'
    }

    test {
        useJUnitPlatform()
    }

    javadoc {
        options.memberLevel = JavadocMemberLevel.PROTECTED
        options.addBooleanOption('html5', true)
    }

    afterReleaseBuild.dependsOn publish
}

dependencies {
    constraints {
        api 'org.assertj:assertj-core:3.16.0'
        api 'org.dom4j:dom4j:2.1.3'
        api 'com.google.code.gson:gson:2.8.6'
        api 'com.fasterxml.jackson.core:jackson-databind:2.11.0'
        api 'jakarta.json:jakarta.json-api:1.1.6'
        api 'org.glassfish:jakarta.json:1.1.6'
        api 'jaxen:jaxen:1.2.0'
        api 'org.jdom:jdom2:2.0.6'
        api 'org.junit.jupiter:junit-jupiter:5.6.2'
        api 'org.mockito:mockito-junit-jupiter:3.3.3'
        api 'xom:xom:1.3.5'
    }
}

release {
    failOnCommitNeeded = false

    git {
        requireBranch = 'java6'
    }
}

wrapper {
    gradleVersion = '6.4'
}
