plugins {
    id 'com.diffplug.spotless' version '6.18.0' apply false
    id 'info.solidsoft.pitest' version '1.9.11' apply false
    id 'io.github.gradle-nexus.publish-plugin' version '1.3.0'
    id 'me.champeau.jmh' version '0.7.0' apply false
    id 'net.ltgt.errorprone' version '3.0.1' apply false
    id 'net.researchgate.release' version '3.0.2'
    id 'org.beryx.jar' version '2.0.0' apply false
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'org.beryx.jar'
    apply from: rootProject.file('gradle/check-checkstyle.gradle')
    apply from: rootProject.file('gradle/check-codecoverage.gradle')
    apply from: rootProject.file('gradle/check-errorprone.gradle')
    apply from: rootProject.file('gradle/publish-jar.gradle')
    apply from: rootProject.file('gradle/publish-maven.gradle')

    sourceCompatibility = JavaVersion.VERSION_1_6
    targetCompatibility = JavaVersion.VERSION_1_6

    repositories {
        mavenCentral()
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    // after gradle 5.3 we need to set this attribute to mark test configuration targeting java 8
    [configurations.testCompileClasspath, configurations.testRuntimeClasspath].each {
        it.attributes {
            attribute(TargetJvmVersion.TARGET_JVM_VERSION_ATTRIBUTE, 11)
        }
    }

    compileJava {
        options.release = 6
        options.compilerArgs << '-Xlint:all,-options' << '-Werror'
    }

    compileTestJava {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
        options.release = 11
    }

    moduleConfig {
        moduleInfoPath = 'src/main/module/module-info.java'
        multiReleaseVersion = 11
        version = project.version
        neverCompileModuleInfo = true
    }

    test {
        useJUnitPlatform()

        reports {
            junitXml.required = true
        }
    }

    javadoc {
        options.memberLevel = JavadocMemberLevel.PROTECTED
        options.addBooleanOption('html5', true)
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username = sonatypeUsername
            password = sonatypePassword
        }
    }
}

release {
    preCommitText = '[skip ci]'
    git {
        requireBranch.set('2.x')
    }
}

wrapper {
    gradleVersion = '8.1'
}
