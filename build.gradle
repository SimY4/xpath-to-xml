plugins {
    id 'net.ltgt.errorprone' version '0.0.13' apply false
}

ext {
    jUnitVersion = '4.12'
    mockitoVersion = '2.12.0'
    assertjVersion = '2.8.0'
    errorproneVersion = '2.1.2'
    checkstyleVersion = '8.4'

    def buildTimeAndDate = new Date()
    buildDate = buildTimeAndDate.format("yyyy-MM-dd")
    buildTime = buildTimeAndDate.format("HH:mm:ss.SSSZ")
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'signing'
    apply plugin: 'net.ltgt.errorprone'
    apply from: rootProject.file('gradle/check-checkstyle.gradle')
    apply from: rootProject.file('gradle/check-codecoverage.gradle')
    apply from: rootProject.file('gradle/publish-jar.gradle')
    apply from: rootProject.file('gradle/publish-maven.gradle')

    sourceCompatibility = JavaVersion.VERSION_1_6

    repositories {
        mavenCentral()
    }

    configurations.errorprone {
        resolutionStrategy.force "com.google.errorprone:error_prone_core:$errorproneVersion"
    }

    dependencies {
        checkstyle "com.puppycrawl.tools:checkstyle:$checkstyleVersion"
    }

    compileJava {
        options.compilerArgs << '-Xplugin:ErrorProne'
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives jar
        archives sourcesJar
        archives javadocJar
    }

    signing {
        required {
            signatory != null && !(project.version =~ /-SNAPSHOT$/)
        }
        sign configurations.archives
    }

}

task wrapper(type: Wrapper) {
    gradleVersion = '4.3.1'
}
