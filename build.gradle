plugins {
    id 'me.champeau.gradle.jmh' version '0.4.4'
}

allprojects {
    group 'com.github.simy4'
    version '0.9.2'

    jmh {
        jmhVersion '1.19'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'findbugs'
    apply plugin: 'me.champeau.gradle.jmh'

    sourceCompatibility = JavaVersion.VERSION_1_8

    repositories {
        mavenCentral()
    }

    configurations {
        provided
    }

    sourceSets {
        main.compileClasspath += configurations.provided
        test.compileClasspath += configurations.provided
        test.runtimeClasspath += configurations.provided
    }

    dependencies {
        checkstyle 'com.puppycrawl.tools:checkstyle:8.0'

        compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    }

    checkstyle {
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
        showViolations = true
        ignoreFailures = false
    }

    findbugs {
        toolVersion = "3.0.1"
        sourceSets = [sourceSets.main]
        ignoreFailures = true
        reportsDir = file("$project.buildDir/reports/findbugs")
        effort = "max"
        reportLevel = "high"
    }

    findbugsMain {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    findbugsTest {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    jmh {
        include = ['com\\.github\\.simy4\\.xpath\\..+']
        humanOutputFile = null
        iterations = 10
        fork = 2
        failOnError = true
        timeUnit = 'ms'
        warmupIterations = 5
    }

}
