plugins {
    id 'com.diffplug.spotless' version '5.14.0' apply false
    id 'cz.augi.gradle.wartremover' version '0.14.1' apply false
    id 'info.solidsoft.pitest' version '1.6.0' apply false
    id 'java-platform'
    id 'me.champeau.jmh' version '0.6.5' apply false
    id 'net.ltgt.errorprone' version '2.0.2' apply false
    id 'net.researchgate.release' version '2.8.1'
    id 'ru.vyarus.animalsniffer' version '1.5.3' apply false
}

subprojects {
    apply plugin: 'java-library'
    apply from: rootProject.file('gradle/dependencies.gradle')
    apply from: rootProject.file('gradle/animal-sniffer.gradle')
    if (!JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_16)) {
        apply from: rootProject.file('gradle/check-checkstyle.gradle')
    }
    apply from: rootProject.file('gradle/check-codecoverage.gradle')
    apply from: rootProject.file('gradle/check-errorprone.gradle')
    apply from: rootProject.file('gradle/publish-jar.gradle')
    apply from: rootProject.file('gradle/publish-maven.gradle')

    sourceCompatibility = JavaVersion.VERSION_1_8

    repositories {
        mavenCentral()
    }

    configurations {
        internal {
            visible = false
            canBeConsumed = false
            canBeResolved = false
        }
        compileClasspath.extendsFrom internal
        runtimeClasspath.extendsFrom internal
        testCompileClasspath.extendsFrom internal
        testRuntimeClasspath.extendsFrom internal
    }

    dependencies {
        internal platform(rootProject)
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << '-Xlint:all' << '-Xlint:-options' << '-Werror'
    }

    test {
        useJUnitPlatform()
    }

    javadoc {
        options.memberLevel = JavadocMemberLevel.PROTECTED

        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
            options.addStringOption('Xdoclint:all,-missing', '-quiet')
            options.addStringOption('-release', '8') // workaround javadoc issues in Java 9+
        }
    }
}

dependencies {
    constraints {
        api 'org.assertj:assertj-core:3.20.2'
        api 'org.dom4j:dom4j:2.1.3'
        api 'com.google.code.gson:gson:2.8.7'
        api 'com.fasterxml.jackson.core:jackson-databind:2.12.3'
        api 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.12.3'
        api 'jakarta.json:jakarta.json-api:2.0.1'
        api 'org.glassfish:jakarta.json:2.0.1'
        api 'jaxen:jaxen:1.2.0'
        api 'org.jdom:jdom2:2.0.6'
        api 'org.junit.jupiter:junit-jupiter:5.7.2'
        api 'org.mockito:mockito-junit-jupiter:3.11.2'
        api 'xom:xom:1.3.7'
    }
}

release {
    git {
        requireBranch = 'java6'
    }
}

wrapper {
    gradleVersion = '7.1.1'
}
