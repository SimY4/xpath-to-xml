plugins {
    id 'cz.alenkacz.gradle.scalafmt' version '1.14.0' apply false
    id 'cz.augi.gradle.wartremover' version '0.12.0' apply false
    id 'info.solidsoft.pitest' version '1.5.2' apply false
    id 'java-platform'
    id 'me.champeau.gradle.jmh' version '0.5.0' apply false
    id 'net.ltgt.errorprone' version '1.2.1' apply false
    id 'net.researchgate.release' version '2.8.1'
    id 'ru.vyarus.animalsniffer' version '1.5.1' apply false
}

ext {
    jacocoVersion = '0.8.5'

    def buildTimeAndDate = new Date()
    buildDate = buildTimeAndDate.format("yyyy-MM-dd")
    buildTime = buildTimeAndDate.format("HH:mm:ss.SSSZ")
}

subprojects {
    apply plugin: 'java-library'
    apply from: rootProject.file('gradle/dependencies.gradle')
    apply from: rootProject.file('gradle/animal-sniffer.gradle')
    apply from: rootProject.file('gradle/check-checkstyle.gradle')
    apply from: rootProject.file('gradle/check-codecoverage.gradle')
    apply from: rootProject.file('gradle/check-errorprone.gradle')
    apply from: rootProject.file('gradle/publish-jar.gradle')
    apply from: rootProject.file('gradle/publish-maven.gradle')

    sourceCompatibility = JavaVersion.VERSION_1_6

    repositories {
        mavenCentral()
    }

    configurations {
        internal {
            visible = false
            canBeConsumed = false
            canBeResolved = false
        }
        compileClasspath.extendsFrom internal
        runtimeClasspath.extendsFrom internal
        testCompileClasspath.extendsFrom internal
        testRuntimeClasspath.extendsFrom internal
    }

    dependencies {
        internal platform(rootProject)
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    // after gradle 5.3 we need to set this attribute to mark test configuration targeting java 8
    [configurations.testCompileClasspath, configurations.testRuntimeClasspath].each {
        it.attributes {
            attribute(TargetJvmVersion.TARGET_JVM_VERSION_ATTRIBUTE, 8)
        }
    }

    compileTestJava {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << '-Xlint:all' << '-Xlint:-options' << '-Werror'
    }

    test {
        useJUnitPlatform()
    }

    javadoc {
        options.memberLevel = JavadocMemberLevel.PROTECTED

        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
            options.addStringOption('-release', '8') // workaround javadoc issues in Java 9+
        }
    }

    afterReleaseBuild.dependsOn publish
}

dependencies {
    constraints {
        api 'org.assertj:assertj-core:3.16.1'
        api 'org.dom4j:dom4j:2.1.3'
        api 'com.google.code.gson:gson:2.8.6'
        api 'com.fasterxml.jackson.core:jackson-databind:2.11.2'
        api 'jakarta.json:jakarta.json-api:1.1.6'
        api 'org.glassfish:jakarta.json:1.1.6'
        api 'jaxen:jaxen:1.2.0'
        api 'org.jdom:jdom2:2.0.6'
        api 'org.junit.jupiter:junit-jupiter:5.6.2'
        api 'org.mockito:mockito-junit-jupiter:3.5.0'
        api 'xom:xom:1.3.5'
    }
}

release {
    failOnCommitNeeded = false
    
    git {
        requireBranch = 'java6'
    }
}

wrapper {
    gradleVersion = '6.6'
}
