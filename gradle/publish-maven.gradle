apply plugin: 'maven-publish'
apply plugin: 'signing'

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            afterEvaluate {
                artifactId = project.name
                pom {
                    name = project.name
                    description = projectDescription
                    url = 'http://github.com/SimY4/xpath-to-xml'
                    inceptionYear = '2017'

                    scm {
                        connection = 'scm:git:http://github.com/SimY4/xpath-to-xml'
                        developerConnection = 'scm:git:http://github.com/SimY4/xpath-to-xml'
                        url = 'http://github.com/SimY4/xpath-to-xml'
                    }

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'SimY4'
                            name = 'Alex Simkin'
                            url = 'http://github.com/SimY4'
                        }
                    }
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
            url version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = sonatypeUsername
                password = sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
