ext.moduleName = 'com.github.simych.xpath.scala'

apply plugin: 'scala'
apply plugin: 'cz.alenkacz.gradle.scalafmt'
apply plugin: 'cz.augi.gradle.wartremover'
apply from: rootProject.file('gradle/benchmark-jmh.gradle')

buildDir = 'build-2.11'

clean {
    delete 'build-2.11'
}

sourceSets {
    main.scala.srcDirs += [file('src/main/scala-2-'), file('src/main/scala-2.12-')]
}

dependencies {
    api project(':xpath-to-xml-core')

    implementation libraries.scalaLibrary('2.11.12')
    implementation libraries.scalaReflect('2.11.12')
    implementation libraries.scalaXml('2.11')

    testImplementation project(':xpath-to-xml-test')
    testImplementation libraries.junit
    testImplementation libraries.assertj
}

javadocJar {
    dependsOn scaladoc
    from scaladoc
}

scaladoc {
    scalaDocOptions.with {
        additionalParameters = [
                '-language:experimental.macros',     // Allow macro definition (besides implementation and application)
                '-language:implicitConversions',     // Allow definition of implicit functions called views
        ]
    }
}

wartremover {
    warningWarts -= ['DefaultArguments', 'IsInstanceOf', 'Null', 'Throw', 'Var']
    test {
        warningWarts -= ['NonUnitStatements', 'TraversableOps']
    }
}

tasks.withType(ScalaCompile) {
    scalaCompileOptions.with {
        optimize = true
        deprecation = true
        encoding = 'UTF-8'
        additionalParameters = [
                '-explaintypes',                     // Explain type errors in more detail.
                '-feature',                          // Emit warning and location for usages of features that should be imported explicitly.
                '-language:experimental.macros',     // Allow macro definition (besides implementation and application)
                '-language:implicitConversions',     // Allow definition of implicit functions called views
                '-unchecked',                        // Enable additional warnings where generated code depends on assumptions.
                '-Xcheckinit',                       // Wrap field accessors to throw an exception on uninitialized access.
                '-Xfatal-warnings',                  // Fail the compilation if there are any warnings.
                '-Xlint',                            // Turn on linting.
                '-Ywarn-dead-code',                  // Warn when dead code is identified.
                '-Ywarn-numeric-widen',              // Warn when numerics are widened.
                '-Xfuture',                          // Turn on future language features.
                '-Yno-adapted-args',                 // Do not adapt an argument list (either by inserting () or creating a tuple) to match the receiver.
        ]
    }
}
