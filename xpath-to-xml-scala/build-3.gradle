ext.moduleName = 'com.github.simych.xpath.scala'

apply plugin: 'scala'
apply from: rootProject.file('gradle/benchmark-jmh.gradle')

buildDir = 'build-3'

clean {
    delete 'build-3'
}

sourceSets {
    main.scala.srcDirs += [file('src/main/scala-2.13+'), file('src/main/scala-3+')]
}

dependencies {
    api project(':xpath-to-xml-core')

    implementation libs.scala.library.v3
    implementation libs.scala.xml.v3

    scalaCompilerPlugins libs.wartremover.v3

    testImplementation project(':xpath-to-xml-test')
    testImplementation libs.bundles.scalatest.v3
}

javadocJar {
    dependsOn scaladoc
    from scaladoc
}

spotless {
    scala {
        licenseHeaderFile rootProject.file('gradle/license.header'), "package "
    }
}

tasks.withType(ScalaCompile).configureEach {
    scalaCompileOptions.with {
        deprecation = true
        encoding = 'UTF-8'
        additionalParameters = [
                '-project-url', projectUrl,
                '-explain',                             // Explain errors in more detail.
                '-explain-types',                       // Explain type errors in more detail.
                '-feature',                             // Emit warning and location for usages of features that should be imported explicitly.
                '-language:macros,implicitConversions',
                '-Xfatal-warnings',                     // Fail the compilation if there are any warnings.
                '-source:3.2',                          // Turn on future language features.
                '-java-output-version:8',
                '-P:wartremover:only-warn-traverser:org.wartremover.warts.Unsafe',
        ]
    }
}
