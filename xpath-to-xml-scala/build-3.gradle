ext.moduleName = 'com.github.simych.xpath.scala'

apply plugin: 'scala'
apply from: rootProject.file('gradle/benchmark-jmh.gradle')

buildDir = 'build-3'

clean {
    delete 'build-3'
}

sourceSets {
    main.scala.srcDirs += [file('src/main/scala-2.13+'), file('src/main/scala-3+')]
}

dependencies {
    api project(':xpath-to-xml-core')

    implementation 'org.scala-lang:scala3-library_3:3.0.2'
    implementation libraries.scalaXml('3')

    testImplementation project(':xpath-to-xml-test')
    testImplementation libraries.scalatest('3')
    testImplementation 'co.helmethair:scalatest-junit-runner'
    testImplementation 'org.junit.platform:junit-platform-engine'
}

javadocJar {
    dependsOn scaladoc
    from scaladoc
}

scaladoc {
    scalaDocOptions.with {
        additionalParameters = [
                '-language:macros,implicitConversions'
        ]
    }
}

tasks.withType(ScalaCompile) {
    scalaCompileOptions.with {
        deprecation = true
        encoding = 'UTF-8'
        additionalParameters = [
                '-project-url',     projectUrl,
                '-explain',                             // Explain errors in more detail.
                '-explain-types',                       // Explain type errors in more detail.
                '-feature',                             // Emit warning and location for usages of features that should be imported explicitly.
                '-language:macros,implicitConversions',
                '-Xfatal-warnings',                     // Fail the compilation if there are any warnings.
                '-source:3.0',                          // Turn on future language features.
                '-Xtarget:8',
        ]
    }
}
