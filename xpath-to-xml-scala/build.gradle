ext {
    moduleName = 'com.github.simych.xpath.scala'
    allScalaVersions = true
    scalaVersions = '2.11.12, 2.12.7, 2.13.0-M5'
    defaultScalaVersions = '2.12.7'
}

apply plugin: 'scala'
apply plugin: 'com.adtran.scala-multiversion-plugin'
apply from: rootProject.file('gradle/benchmark-jmh.gradle')

configurations {
    scalaCompilerPlugin
}

dependencies {
    api project(':xpath-to-xml-core')

    implementation 'org.scala-lang:scala-library:%scala-version%'
    if (scalaVersion.startsWith('2.13')) {
        implementation 'org.scala-lang.modules:scala-xml_2.13.0-M5:[1.0, 2.0['

        scalaCompilerPlugin 'com.olegpy:better-monadic-for_2.13.0-M5:[0.3,0.4['
    } else {
        implementation 'org.scala-lang.modules:scala-xml_%%:[1.0, 2.0['

        scalaCompilerPlugin 'com.olegpy:better-monadic-for_%%:[0.3,0.4['
    }

    testImplementation project(':xpath-to-xml-test')
    if (scalaVersion.startsWith('2.13')) {
        testImplementation 'org.scala-lang.modules:scala-java8-compat_2.13.0-M5:[0.9,1.0['
    } else {
        testImplementation 'org.scala-lang.modules:scala-java8-compat_%%:[0.9,1.0['
    }
    testImplementation "org.junit.jupiter:junit-jupiter-api:$jUnitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$jUnitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"
    testImplementation "org.assertj:assertj-core:$assertjVersion"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$jUnitVersion"
}

tasks.withType(ScalaCompile) {
    if (scalaVersion.startsWith('2.11')) {
        scalaCompileOptions.optimize = true
    }
    scalaCompileOptions.additionalParameters = [
            '-Xplugin:' + configurations.scalaCompilerPlugin.asPath,
            '-deprecation',                      // Emit warning and location for usages of deprecated APIs.
            '-encoding', 'utf-8',                // Specify character encoding used by source files.
            '-explaintypes',                     // Explain type errors in more detail.
            '-feature',                          // Emit warning and location for usages of features that should be imported explicitly.
            '-language:implicitConversions',     // Allow definition of implicit functions called views
            '-unchecked',                        // Enable additional warnings where generated code depends on assumptions.
            '-Xcheckinit',                       // Wrap field accessors to throw an exception on uninitialized access.
            '-Xfatal-warnings',                  // Fail the compilation if there are any warnings.
            '-Xfuture',                          // Turn on future language features.
            '-Xlint:adapted-args',               // Warn if an argument list is modified to match the receiver.
            '-Xlint:delayedinit-select',         // Selecting member of DelayedInit.
            '-Xlint:doc-detached',               // A Scaladoc comment appears to be detached from its element.
            '-Xlint:inaccessible',               // Warn about inaccessible types in method signatures.
            '-Xlint:infer-any',                  // Warn when a type argument is inferred to be `Any`.
            '-Xlint:missing-interpolator',       // A string literal appears to be missing an interpolator id.
            '-Xlint:nullary-override',           // Warn when non-nullary `def f()' overrides nullary `def f'.
            '-Xlint:nullary-unit',               // Warn when nullary methods return Unit.
            '-Xlint:option-implicit',            // Option.apply used implicit view.
            '-Xlint:package-object-classes',     // Class or object defined in package object.
            '-Xlint:poly-implicit-overload',     // Parameterized overloaded implicit methods are not visible as view bounds.
            '-Xlint:private-shadow',             // A private field (or class parameter) shadows a superclass field.
            '-Xlint:stars-align',                // Pattern sequence wildcard must align with sequence component.
            '-Xlint:type-parameter-shadow',      // A local type parameter shadows a type already in scope.
            '-Ywarn-dead-code',                  // Warn when dead code is identified.
    ] + (!scalaVersion.startsWith('2.11') ? ['-opt:l:inline', '-opt-inline-from:**'] : [])
}

compileTestScala {
    scalaCompileOptions.additionalParameters << '-target:jvm-1.8'
}