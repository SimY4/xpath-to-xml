ext.moduleName = 'com.github.simych.xpath.scala'

apply plugin: 'scala'
apply plugin: 'cz.augi.gradle.wartremover'

buildDir = 'build-2.12'

clean {
    delete 'build-2.12'
}

sourceSets {
    main.scala.srcDirs += [file('src/main/scala-2-'), file('src/main/scala-2.12-')]
}

dependencies {
    api project(':xpath-to-xml-core')

    implementation libraries.scalaLibrary('2.12.15')
    implementation libraries.scalaReflect('2.12.15')
    implementation libraries.scalaXml('2.12')

    testImplementation project(':xpath-to-xml-test')
    testImplementation libraries.scalatest('2.12')
    testImplementation 'co.helmethair:scalatest-junit-runner'
    testImplementation 'org.junit.platform:junit-platform-engine'
}

javadocJar {
    dependsOn scaladoc
    from scaladoc
}

spotless {
    scala {
        scalafmt('3.0.6').configFile('.scalafmt.conf')
        licenseHeaderFile rootProject.file('gradle/license.header'), "package "
    }
}

scaladoc {
    scalaDocOptions.with {
        additionalParameters = [
                '-language:experimental.macros',     // Allow macro definition (besides implementation and application)
                '-language:implicitConversions',     // Allow definition of implicit functions called views
        ]
    }
}

wartremover {
    warningWarts -= ['DefaultArguments', 'IsInstanceOf', 'Null', 'Throw', 'Var']
    test {
        warningWarts -= ['TraversableOps']
    }
}

tasks.withType(ScalaCompile) {
    scalaCompileOptions.with {
        deprecation = true
        encoding = 'UTF-8'
        additionalParameters = [
                '-explaintypes',                     // Explain type errors in more detail.
                '-feature',                          // Emit warning and location for usages of features that should be imported explicitly.
                '-language:experimental.macros',     // Allow macro definition (besides implementation and application)
                '-language:implicitConversions',     // Allow definition of implicit functions called views
                '-unchecked',                        // Enable additional warnings where generated code depends on assumptions.
                '-Xcheckinit',                       // Wrap field accessors to throw an exception on uninitialized access.
                '-Xfatal-warnings',                  // Fail the compilation if there are any warnings.
                '-Xlint:adapted-args',               // Warn if an argument list is modified to match the receiver.
                '-Xlint:by-name-right-associative',  // By-name parameter of right associative operator.
                '-Xlint:constant',                   // Evaluation of a constant arithmetic expression results in an error.
                '-Xlint:delayedinit-select',         // Selecting member of DelayedInit.
                '-Xlint:doc-detached',               // A Scaladoc comment appears to be detached from its element.
                '-Xlint:inaccessible',               // Warn about inaccessible types in method signatures.
                '-Xlint:infer-any',                  // Warn when a type argument is inferred to be `Any`.
                '-Xlint:missing-interpolator',       // A string literal appears to be missing an interpolator id.
                '-Xlint:nullary-override',           // Warn when non-nullary `def f()' overrides nullary `def f'.
                '-Xlint:nullary-unit',               // Warn when nullary methods return Unit.
                '-Xlint:option-implicit',            // Option.apply used implicit view.
                '-Xlint:package-object-classes',     // Class or object defined in package object.
                '-Xlint:poly-implicit-overload',     // Parameterized overloaded implicit methods are not visible as view bounds.
                '-Xlint:private-shadow',             // A private field (or class parameter) shadows a superclass field.
                '-Xlint:stars-align',                // Pattern sequence wildcard must align with sequence component.
                '-Xlint:type-parameter-shadow',      // A local type parameter shadows a type already in scope.
                '-Xlint:unsound-match',              // Pattern match may not be typesafe.
                '-Ywarn-dead-code',                  // Warn when dead code is identified.
                '-Ywarn-numeric-widen',              // Warn when numerics are widened.
                '-Xfuture',                          // Turn on future language features.
                '-Yno-adapted-args',                 // Do not adapt an argument list (either by inserting () or creating a tuple) to match the receiver.
                '-Xsource:3',                        // Turn on future language features.
                '-target:jvm-1.8',
        ]
        if (!JavaVersion.current().isJava12Compatible()) {
            additionalParameters += [
                    '-opt:l:method',
                    '-opt:l:inline',
                    '-opt-inline-from:com.github.simy4.xpath.scala.navigator.**,com.github.simy4.xpath.scala.syntax.**',
            ]
        }
    }
}
